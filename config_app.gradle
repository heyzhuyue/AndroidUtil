apply {
    plugin "com.android.application"
    plugin "kotlin-android"
    plugin "kotlin-android-extensions"
}

configApkName project

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion project.localBuildToolsVersion
    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        applicationId rootProject.applicationId + suffix
        targetSdkVersion rootProject.targetSdkVersion
        resValue "string", "app_name", rootProject.appName + suffix
        multiDexEnabled true
    }

    signingConfigs {
        release {
            keyAlias 'zx1234'
            keyPassword 'zx1234'
            storeFile file('../keys/key_stone')
            storePassword 'zx1234'
            v2SigningEnabled false
            v1SigningEnabled true
        }

        debug {
            storeFile file("../keys/debug.keystore")
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
        dev{
            debuggable true
            minifyEnabled false
            matchingFallbacks = ['release', 'debug']
            signingConfig signingConfigs.debug
        }
        pre{
            debuggable true
            minifyEnabled false
            matchingFallbacks = ['release', 'debug']
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

    }

    lintOptions {
        abortOnError false
    }
}

private String getSuffix() {
    if (project.name == "launcher-app") return ""
    String[] splits = project.name.split("-")
    String suffix = ""
    if (splits.length == 2) {
        suffix = "_" + splits[0]
    }
    print("suffix" + suffix)
    return suffix
}

/**
 * 配置Apk名字
 * @param pro Project项目
 * @return
 */
def configApkName(Project pro) {
    pro.android.applicationVariants.all { variant ->
        if (variant.buildType.name != "debug") {
            variant.getPackageApplication().outputDirectory = new File("${rootDir.path}/apk")
            variant.getPackageApplication().outputScope.apkDatas.forEach { apkData ->
                apkData.outputFileName = "util" + suffix +
                        "_" + variant.versionName.replace(".", "_") +
                        ".apk"
            }
        }
    }
}